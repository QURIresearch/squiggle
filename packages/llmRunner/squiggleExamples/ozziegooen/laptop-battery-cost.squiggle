@name("Charging Efficiency % (A Constant)")
chargingEfficiency = 0.7 to 0.9

@name("Fn: Cost to Charge Battery Once")
costToCharge(batteryCapacity, electricityRate, chargingEfficiency) = {
  loadInkWh = batteryCapacity / 1000
  chargeCost = loadInkWh * electricityRate / chargingEfficiency
  chargeCost
}

@name("Fn: Calculate Total Costs")
calcCosts(
  batteryCapacity,
  electricityRate,
  batteryCycles,
  batteryCost,
  hoursOfUsePerCycle
) = {
  electricityCostPerCycle = costToCharge(
    batteryCapacity,
    electricityRate,
    chargingEfficiency
  )
  batteryCostPerCycle = batteryCost / batteryCycles

  @name("Costs per Full Battery Cycle")
  perCycleCosts = Plot.dists(
    {
      dists: [
        { name: "Electricity Cost", value: electricityCostPerCycle },
        { name: "Battery Depreciation", value: batteryCostPerCycle },
      ],
      xScale: Scale.linear({ tickFormat: "$.2f" }),
    }
  )

  @name("Costs per Hour of Use")
  perHourCosts = Plot.dists(
    {
      dists: [
        {
          name: "Electricity Cost",
          value: electricityCostPerCycle / hoursOfUsePerCycle,
        },
        {
          name: "Battery Depreciation",
          value: batteryCostPerCycle / hoursOfUsePerCycle,
        },
      ],
      xScale: Scale.linear({ tickFormat: "$.2f" }),
    }
  )

  { perCycleCosts, perHourCosts }
}

@name("Calculator")
export calculator = Calculator(
  calcCosts,
  {
    title: "Laptop Usage Costs: Electricity & Battery",
    description: "This calculator estimates the combined costs of electricity consumption and battery depreciation for laptop use. It assumes the laptop is running on battery power.

Costs are provided per **full battery cycle** and per **hour of use**.",
    inputs: [
      Input.text(
        {
          name: "Battery Capacity (Watt-Hours)",
          default: "60",
          description: "E.g., A 14-inch M1 MacBook Pro has ~60 Wh battery.",
        }
      ),
      Input.text(
        {
          name: "Electricity Rate ($ per kWh)",
          description: "E.g., In California, this was ~$0.20 in 2022.",
          default: "0.2 to 0.3",
        }
      ),
      Input.text(
        {
          name: "Battery Lifespan (Cycles)",
          description: "Number of full charge cycles before battery replacement is needed.",
          default: "1000 to 1500",
        }
      ),
      Input.text(
        {
          name: "Battery Replacement Cost ($)",
          description: "Include both monetary cost and inconvenience factor.",
          default: "100 to 200",
        }
      ),
      Input.text(
        {
          name: "Battery Life per Charge (Hours)",
          description: "Typical usage time on a single charge.",
          default: "3 to 5",
        }
      ),
    ],
  }
)
